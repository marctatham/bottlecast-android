plugins {

    id("com.android.application")
    id("com.google.gms.google-services")

    id("kotlin-android")
    id("kotlin-kapt")
    id 'kotlin-parcelize'
}

// load up the external properties - for signing configuration
Properties props = new Properties()
try {
    props.load(file('properties.properties').newDataInputStream())
} catch (Exception exception) {
    throw new GradleException("properties.properties not found", exception)
}

android {
    compileSdkVersion 33

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.koala.messagebottle"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        def keyStorePassword = "${props.getProperty("keyStorePassword")}"
        def keyStoreAlias = "${props.getProperty("keyStoreAlias")}"
        def keyStoreAliasPassword = "${props.getProperty("keyStoreAliasPassword")}"

        debug {
            storeFile file("message-bottle-debug.jks")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            storeFile file("message-bottle-release.jks")
            storePassword keyStorePassword
            keyAlias keyStoreAlias
            keyPassword keyStoreAliasPassword
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    namespace 'com.koala.messagebottle'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX support / architecture components
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.core:core-ktx:$versionCoreKtx"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versionLifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versionLifecycle"
    implementation "androidx.fragment:fragment-ktx:$versionFragment"

    implementation("androidx.navigation:navigation-fragment-ktx:$version_nav")
    implementation("androidx.navigation:navigation-ui-ktx:$version_nav")
    implementation("androidx.navigation:navigation-dynamic-features-fragment:$version_nav")
    implementation("androidx.navigation:navigation-compose:$version_nav_compose") // not in use YET

    // kotlin ktx
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.30'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.5.30"

    // better logging
    implementation "com.jakewharton.timber:timber:$versionTimber"

    // okhttp
    api "com.squareup.okhttp3:okhttp:$versionOkHttp"
    api "com.squareup.okhttp3:logging-interceptor:$versionOkHttp"

    // moshi
    api "com.squareup.moshi:moshi-kotlin:$versionMoshi"
    api "com.squareup.moshi:moshi-adapters:$versionMoshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versionMoshi"

    // retrofit + moshi
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // Dagger
    implementation "com.google.dagger:dagger-android:$versionDagger"
    implementation "com.google.dagger:dagger-android-support:$versionDagger"
    kapt "com.google.dagger:dagger-compiler:$versionDagger"
    kapt "com.google.dagger:dagger-android-processor:$versionDagger"

    // Using Dagger in androidTest and Robolectric too
    kaptAndroidTest "com.google.dagger:dagger-compiler:$versionDagger"
    kaptTest "com.google.dagger:dagger-compiler:$versionDagger"

    implementation 'com.google.firebase:firebase-analytics:19.0.1'
    implementation 'com.google.firebase:firebase-auth:21.0.1'
    implementation 'com.google.firebase:firebase-auth-ktx:21.0.1'

    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.google.android.material:material:1.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versionKotlinXCoroutinesCore"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$versionKotlinX"

    implementation "com.airbnb.android:lottie:$versionLottie"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    androidTestImplementation("androidx.navigation:navigation-testing:$version_nav")
}
